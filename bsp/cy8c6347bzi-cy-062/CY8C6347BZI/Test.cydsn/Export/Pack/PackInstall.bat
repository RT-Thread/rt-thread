@echo off
REM This script extracts a CMSIS-Pack file to the specified destination CMSIS root folder
REM Usage: PackInstall.bash packFile.pack [destFolder] [-force]
REM    /force option removes/overwrites existing destination filespri
REM Note that Pack files do not have to be installed in a Keil installation based folder.
REM
REM This script requires that you have 'unzip' available via your PATH environment variable

set /A "FORCE=0"
set INPUT=
set OUTPUT=

REM Count script arguments
set /A "argC=0"
for %%x in (%*) do Set /A argC+=1
REM Check that number of arguments is between 1 and 3
set /A "tempvar=0"
if %argC% LSS 1 Set /A "tempvar=1"
if %argC% GTR 3 Set /A "tempvar=1"
if %tempvar% EQU 1 (
    CALL :printUsage
    EXIT /B 201
)

REM Detect optional arguments
set EXITYES=
set OUTPUTRAW=
:loop
if not "%~1"=="" (
    if "%~1"=="/force" (
        set /A "FORCE=1"
    ) else (
	    if [%INPUT%]==[] (
		    set INPUT="%~1"
	    ) else (
            if [%OUTPUT%]==[] (
		        set OUTPUT="%~1"
				set OUTPUTRAW=%~1
		    ) else (
			    echo Extraneous argument detected: %~1
				set EXITYES=YES
				CALL :printUsage
			)
	    )
	)
	
    SHIFT
    GOTO :loop
)
if "%EXITYES%"=="YES" (
    EXIT /B 201	
)

REM Does the input file exist?
if not exist %INPUT% (
   ECHO Could not find input file %INPUT%
   EXIT /B 202
)

REM Extract input file leaf name
set FILENAME=%INPUT%
for %%A in (%INPUT%) do (
    Set FILENAME=%%~nxA
)

REM Separate out Vendor, PackName and Version strings
for /f "tokens=1,2,3,4,5,6 delims=." %%a in ("%FILENAME%") do ( set VENDORSTR=%%a&set NAMESTR=%%b&set V1=%%c&set V2=%%d&set V3=%%e)
set VERSIONSTR=
if not [%V1%] == [] set VERSIONSTR=%V1%
if not [%V2%] == [] set VERSIONSTR=%VERSIONSTR%.%V2%
if not [%V3%] == [] set VERSIONSTR=%VERSIONSTR%.%V3%

REM Use current working directory if destination folder not specified
set DESTFILENAME=
set DESTPDSCNAME=
if [%OUTPUT%] == [] (
    set DESTDIR="%cd%\%VENDORSTR%\%NAMESTR%\%VERSIONSTR%"
    set DESTFILENAME="%cd%\%VENDORSTR%\%NAMESTR%\%VERSIONSTR%\%FILENAME%.zip"
    set DESTPDSCNAME="%cd%\%VENDORSTR%\%NAMESTR%\%VERSIONSTR%\*.pdsc"
) else (
    set DESTDIR="%OUTPUTRAW%\%VENDORSTR%\%NAMESTR%\%VERSIONSTR%"
    set DESTFILENAME="%OUTPUTRAW%\%VENDORSTR%\%NAMESTR%\%VERSIONSTR%\%FILENAME%.zip"
    set DESTPDSCNAME="%OUTPUTRAW%\%VENDORSTR%\%NAMESTR%\%VERSIONSTR%\*.pdsc"
)
 
REM Verify input file name format
set /A "tempvar=0"
if [%VENDORSTR%] == [] set /A "tempvar=1"
if [%NAMESTR%] == [] set /A "tempvar=1"
if [%VERSIONSTR%] == [] set /A "tempvar=1"
if %tempvar% EQU 1 (
    ECHO Pack file name not in standard format: VENDOR.PACKNAME.VER1.VER2.VER3.pack
    EXIT /B 203
)

REM Does the destination folder exist, if specified?
if %FORCE% EQU 1 (
    if exist %DESTDIR% (
        REM Output folder exists and user has requested forcible overwrite
        RMDIR /S /Q %DESTDIR%
        if exist %DESTDIR% (
            ECHO Could not remove existing output folder contents
            EXIT /B %ERRORLEVEL%
        )
    )
)

if not exist %DESTDIR% (
    REM Create the destination folder as it doesn't yet exist
    MKDIR %DESTDIR%
	if not exist %DESTDIR% (
        ECHO Could not create destination folder at %DESTDIR%
        EXIT /B %ERRORLEVEL%
    )
) else (
    echo Destination folder already exists at %DESTDIR%
	echo Please use the /force option to overwrite it
	EXIT /B 205
)  

REM Copy the input file to destination folder, renaming it with a ZIP extension, suppressing output
COPY /Y %INPUT% %DESTFILENAME% >nul 2>&1
if %ERRORLEVEL% NEQ 0 (
   ECHO Copying the pack file failed
   EXIT /B %ERRORLEVEL%
)

REM Unzip the archive file
START /B /WAIT unzip -qq -o %DESTFILENAME% -d %DESTDIR%
if %ERRORLEVEL% NEQ 0 (
   ECHO Extracting the pack file failed. Please verify it is a valid CMSIS Pack file.
   EXIT /B %ERRORLEVEL%
)
REM And remove the archive file we copied over, suppressing output
if exist %DESTFILENAME% (
   DEL /F /Q %DESTFILENAME% >nul 2>&1
)
if %ERRORLEVEL% NEQ 0 (
   ECHO Removing the temporary archive file failed
   EXIT /B %ERRORLEVEL%
)

REM Simple validation check - was a PDSC file extracted?
Set /A "tempvar=1"
for %%a in (%DESTPDSCNAME%) do (
   Set /A "tempvar=0"
)
if %tempvar% EQU 1 (
   ECHO Did not find a PDSC file in extracted files. Please verify the input pack file is a valid CMSIS Pack file.
   EXIT /B 206
)

REM And we're done...
EXIT /B 0

REM Functions
:printUsage
ECHO Usage: PackInstall.bat file.pack [destFolder] [/force]
ECHO    file.pack : CMSIS-Pack file generated by PSoC Creator
ECHO    destFolder : output folder [opional]
ECHO    /force : force overwrite of existing output folder contents [optional]
EXIT /B 0
