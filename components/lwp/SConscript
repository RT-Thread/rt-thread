Import('rtconfig')
from building import *

cwd     = GetCurrentDir()
src     = []
CPPPATH = [cwd]

support_arch  = {"arm": ["cortex-m3", "cortex-m4", "cortex-m7", "arm926", "cortex-a"]}
platform_file = {'armcc': 'rvds.S', 'gcc': 'gcc.S', 'iccarm': 'iar.S'}

if rtconfig.PLATFORM in platform_file.keys():	# support platforms
    if rtconfig.ARCH in support_arch.keys() and rtconfig.CPU in support_arch[rtconfig.ARCH]:
        # arch/arm/cortex-m7/lwp_gcc.S
        asm_path = 'arch/' + rtconfig.ARCH + '/' + rtconfig.CPU + '/*_' + platform_file[rtconfig.PLATFORM]
        src = Glob('*.c') + Glob(asm_path)
        CPPPATH = [cwd]

group = DefineGroup('lwP', src, depend = ['RT_USING_LWP'], CPPPATH = CPPPATH)

Return('group')
