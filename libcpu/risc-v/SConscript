# RT-Thread building script for bridge

import os
from building import *

Import('rtconfig')

common64_arch = ['virt64', 'c906', 'c908','ur-cp100']
cwd   = GetCurrentDir()
group = []
list  = os.listdir(cwd)
bsp_path = Dir('#').abspath

if not os.path.exists(bsp_path + "/link.lds"):
    Env['LINKFLAGS'] = Env['LINKFLAGS'].replace('link.lds', cwd + "/link.lds")
    Preprocessing("link.lds.S", ".lds", CPPPATH=[bsp_path, cwd + '/common64/include'])

# fix the linker with crtx.o
Env['LINKFLAGS'] += ' -nostartfiles'

# add common code files
if rtconfig.CPU in common64_arch :
    group += SConscript(os.path.join('common64', 'SConscript'))
else :
    group += SConscript(os.path.join('common', 'SConscript'))

group += SConscript(os.path.join('vector', 'SConscript'))

# cpu porting code files
if 'VENDOR' in vars(rtconfig) and rtconfig.VENDOR != '':
    group = group + SConscript(os.path.join(rtconfig.VENDOR, rtconfig.CPU, 'SConscript'))
elif rtconfig.CPU in list:
    group = group + SConscript(os.path.join(rtconfig.CPU, 'SConscript'))

Return('group')
